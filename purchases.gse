
// ---------------------------------------------------------
// Linking
=ArrayFormula(IF(B5:B<>"",
  HYPERLINK(B5:B,IF(REGEXMATCH(B5:B,"com.mx"),"ðŸ‡²ðŸ‡½","ðŸ‡ºðŸ‡¸")),)
)

=ArrayFormula(IF(B5:B<>"",
  HYPERLINK(
    REGEXREPLACE(
    REGEXREPLACE(
    REGEXREPLACE(
      IF(REGEXMATCH(B5:B, "\.com\.mx"),
        REGEXREPLACE(B5:B, "\.com\.mx", ".com"),
        REGEXREPLACE(B5:B, "\.com",     ".com.mx")),
    "www\.",""),
    "\?[^\?]+$",""),
    "/ref=[^&]+$","/"),
    IF(REGEXMATCH(B5:B,"com.mx"),"ðŸ‡ºðŸ‡¸","ðŸ‡²ðŸ‡½"))
  ,))

=ArrayFormula(IF(J5:J<>"",
  REGEXREPLACE(
  REGEXREPLACE(
  REGEXREPLACE(
    IF(REGEXMATCH(J5:J, "\.com\.mx"),
      REGEXREPLACE(J5:J, "\.com\.mx", ".com"),
      REGEXREPLACE(J5:J, "\.com",     ".com.mx")),
  "www\.",""),
  "\?[^\?]+$",""),
  "/ref=[^&]+$","/"),
))

// ---------------------------------------------------------
// BOOK_INFO
// Given a raw copy-paste from the Amazon's book header, extract its author, title, subtitle, edition type & published date

=LAMBDA(raw,
  SPLIT(
      LAMBDA(title, duraBlanda, published, authors,
        REGEXREPLACE(
        REGEXREPLACE(
          authors, 
          "\([^)]+\)",""),
          "\s+,", ",")  &"â€–"&
        REGEXREPLACE(
        REGEXREPLACE(
          title & IF(REGEXMATCH(title, ":"),"",":--"),
          ":", "â€–"),
          "\([^)]+\)","") &"â€–"&
        REGEXREPLACE(duraBlanda,"Pasta","") &"â€–"&
        REGEXEXTRACT(published, "\d\d\d\d")
    )(
      REGEXEXTRACT(raw,"(^.*?)(?:Hardcover|Paperback|Pasta|\n)"),
      IFERROR(REGEXEXTRACT(raw,"Hardcover|Paperback|Pasta dura|Pasta blanda"),"--"),
      IFERROR(REGEXEXTRACT(raw,"â€“ (.+)\n"),"2022"),
      REGEXEXTRACT(TRIM(raw),"(?:por|by) (.+)$")
    ), "â€–"))( raw_arg )

=LAMBDA(raw,
  SPLIT(
      LAMBDA(title, duraBlanda, published, authors,
        REGEXREPLACE(
        REGEXREPLACE(
          authors, 
          "\([^)]+\)",""),
          "\s+,", ",")  &"â€–"&
        REGEXREPLACE(
          title & IF(REGEXMATCH(title, ":|\."),"",":--"),
          ":|\.", "â€–") &"â€–"&
        duraBlanda &"â€–"&
        REGEXREPLACE(published, "^[^,]+,","")
    )(
      REGEXEXTRACT(raw,"(^.*)Pasta"),
      REGEXEXTRACT(raw,"Pasta (\w+)"),
      REGEXEXTRACT(raw,"â€“ (.+)\n"),
      REGEXEXTRACT(TRIM(raw),"por (.+)$")
    ), "â€–"))( raw_arg )