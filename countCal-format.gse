// ---------------------------------------------------------
// Conditional formatting - Labelled

// isCount|Check / isPast /hasBeenChecked 

// S.co: Since counts: Show past days that have no checks to the right. Since days.
=N("S.co")+AND(
  $C9="count",
  E$5<0,
  COUNTIF($E8:E8, TRUE)>0
)

// S.ch: Since checks
=N("S.ch")+AND(
  $C9="check",
  E$5<0,
  COUNTIF($E9:E9, TRUE)>0
)

//Hide days that have no checks to the left and are in the past
=N("S.unch.co")+AND(
  $C7="count",
  COUNTIF($E6:E6, TRUE)=0,
  E$3<0
)

// U.ch: Until check: Show past days that have no checks to the right. Until days.
=N("U.ch")+AND(
  $C9="check",
  E$5>0,
  COUNTIF(E9:9, TRUE)>0
)

// Clear count of unchecked if today
// isCount / isToday / isAboveUnchecked / notExtendToggle / notFutureChecked
=N("T.unch.co")+AND(
  $C7="count",
  E$5=TODAY(),
  NOT(E6),
  NOT($A7),
  COUNTIF(E6:6, TRUE)=0 
)

// Check coloring if today
// isCheck / isToday / isChecked
=N("T.ch")+AND(   
  $C1="check", 
  E$5=TODAY(),
  E1
)

// Color column bkg if today
=N("T.bkg")+AND(
  E$5=TODAY()
)

// T.unch.ch: Lowlight unchecked checkboxes in today columns
=N("T.unch.ch")+AND(
  $C7="check", 
  E$5=TODAY(),
  NOT(E7)
)

// T.h1: Header
=N("T.h1")+AND(
  E$5=TODAY()
)

// Weekend
=N("Weekend")+OR(
  WEEKDAY(E$5)=7,
  WEEKDAY(E$5)=1
)

// Check coloring
=N("ch.ch")+AND(
  $C7="check",
  E7
)

//Check coloring of first check
=N("ch.ch:1")+AND(
  $C7="check",
  E7,
  COUNTIF($E7:E7, TRUE)=1,
)


// Color check counts
=N("ch.co")+AND(
  $C7="count",
  ISLOGICAL(E6),
  E6
)

//Color first check count
=N("ch.co:1")+AND(
  $C7="count",
  ISLOGICAL(E6),
  E6,
  COUNTIF($E6:E6, TRUE)=1,
)

// Hide days that have no checks to the right and are not in an extend count
=N("U.unch.co")+AND(
  $C7="count",
  NOT($A7),
  COUNTIF(E6:6, TRUE)=0
)