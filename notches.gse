// Notches Sheet
// URL: https://docs.google.com/spreadsheets/d/1Fahc8gYJAGWklCMwxrh6ihHLQ4M8Vq8On8P7cAHPiPY/edit#gid=1853869321

* XY, _X, _Y, XY_ABS imported from Sessions
  * X_ABS, Y_ABS, Y_ABS_REF
    * GET/SET_COL_LABEL
      * IS_DAY
  * Y_REF
    * SET_ROW_LABEL
* DAY_LEFT
* DAY_COUNT
  * NOT_TODAY
* PAST_WEEK_DAYS
  * WEEK_SUMMARY

// ---------------------------------------------------------
// WEEK_SUMMARY
// For the whole column, return count of checked days for the week and below the max count reached.
=LAMBDA(week, {
    COUNTIF(week,TRUE);
    IF(
      COUNTIF(week,TRUE),
      MAX(FILTER(
          INDEX(PAST_WEEK_DAYS(),2),
          INDEX(PAST_WEEK_DAYS(),1)
        ))
    ,)
  })(
    PAST_WEEK_DAYS()
  )

//original at BM7 & below
=COUNTIF(BF7:BL7,TRUE)
=IF(EQ(BM7, 0),,MAXIFS(BF8:BL8,BF7:BL7,TRUE))

// ---------------------------------------------------------
//RANGE(r1, c1, r2, c2)
// Returns the range bound by r1c1 and r2c2. Any coordinate can start with "$" for absolute value.
// Huh, instead of having _X & X_ABS I could have a checker so _X("$0") = X_ABS(0)

// ---------------------------------------------------------
//PAST_WEEK_DAYS
// Returns a range that's 7 cols left from & 1 row more than the current cell. Filtering out columsn that are not days.
=LAMBDA(week, 
  FILTER(
    week,
    INDEX(
      MAP(week,
        LAMBDA(cell,
          IS_DAY(COL(cell))
        )
      )
    , 1)
  )
)(
  INDIRECT(
    "R"&ROW()&"C"&(COLUMN()-7)&
    ":"&
    "R"&(ROW()+1)&"C"&(COLUMN()-1),
    FALSE
  )
)

=LAMBDA(week, 
  FILTER(
    week,
    MAP(week,
      LAMBDA(cell,
        IS_DAY(COL(cell))
      )
    )
  )
)(
  INDIRECT(
    "R"&ROW()&"C"&(COLUMN()-7)&
    ":"&
    "R"&ROW()&"C"&(COLUMN()-1),
    FALSE
  )
)

=INDIRECT(
  "R"&ROW()&"C"&(COL()-7)&
  ":"&
  "R"&ROW()&"C"&COL(),
  FALSE
)

// ---------------------------------------------------------
// SET_ROW_LABEL(cell_ref)
// Lambda for labelling the rows as check/count/note. To be used with MAP.
=IF(ISLOGICAL(cell_ref),
  "check",
  IF(ISLOGICAL(Y_REF(cell_ref, -1)),
    "count",
    IF(ISLOGICAL(Y_REF(cell_ref, -2)),
    "note",)
  ))

// ---------------------------------------------------------
// Y_REF(cell_ref, y)
// From the given cell reference, move only vertically, changing rows but staying in the same col. Upwards is negative, downwards is positive. Variant of Y useful in MAP & ARRAY_FORMULA. See also Y_ABS & Y_ABS_REF.
=INDIRECT(
  "R"& (ROW(cell_ref)+y) &""&
  "C"& COL(cell_ref) &"", 
  FALSE)

// ---------------------------------------------------------
// DAY_COUNT()
// Increment the count if today is true. Otherwise can return blank, 0, or prev+1 depending on context handled by NOT_TODAY. Assumes 

//3rd pass
=LAMBDA( today, prev, 
    IF(today,
      prev+1,
      NOT_TODAY(prev)
    )
  )(
    _Y(-1),
    PREV_DAY()
  )

//2nd pass
=LAMBDA(
    today, 
    prev, 
    extendToggle,
    streakStartsNext,
    IF(today,
      prev+1,
      IF(streakStartsNext,
        "",
        IF(IS_ZERO(prev),
          0,
          IF(extendToggle,
            prev+1,
          )
      ))
    )
  )(
    _Y(-1),
    PREV_DAY(),
    X_ABS(1),
    AND( NOT(_Y(-1)), XY(1,-1) )
  )

//first pass
=IF(
    AND(
        NOT(_Y(-1)),
        XY(1,-1)
      ),
    ,
    IF(_Y(-1),
      AU7+1,
      IF(AND(NOT(ISBLANK(AU7)),EQ(AU7,0)),
        0,
        IF(X_ABS(1),
          AU7+1,
    ))))

// Original formula on AW7
=IF(
  AND(NOT(AW6),AX6),
  ,
  IF(AW6,
    AU7+1,
    IF(AND(NOT(ISBLANK(AU7)),EQ(AU7,0)),
      0,
      IF($A7,
        AU7+1,
  ))))

// ---------------------------------------------------------
// NOT_TODAY
// What do we say to death? Can return blank, 0 or prev+1 depending on context.

//optimization
=LAMBDA(
    prev, 
    streakStartsNext,
    IF(streakStartsNext,
      "",
      IF(IS_ZERO(prev),
        0,
        IF(X_ABS(1),
          prev+1,
        )
    ))
  )(
    prev_arg,
    LAMBDA(up, upRight, 
      AND(
        ISLOGICAL(up),
        ISLOGICAL(upRight),
        NOT(up), 
        upRight 
      )
    )(_Y(-1), XY(1,-1))
  )
=LAMBDA(
    prev, 
    streakStartsNext,
    IF(streakStartsNext,
      "",
      IF(IS_ZERO(prev),
        0,
        IF(X_ABS(1),
          prev+1,
        )
    ))
  )(
    prev_arg,
    AND( NOT(_Y(-1)), XY(1,-1) )
  )

=LAMBDA(
    prev, 
    extendToggle,
    streakStartsNext,
    IF(streakStartsNext,
      "",
      IF(IS_ZERO(prev),
        0,
        IF(extendToggle,
          prev+1,
        )
    ))
  )(
    PREV_DAY(),
    X_ABS(1),
    AND( NOT(_Y(-1)), XY(1,-1) )
  )

// ---------------------------------------------------------
// IS_ZERO
// Check value is zero. Not blank, zero!
=AND(
    NOT(ISBLANK(value)),
    EQ(value,0)
  )

// ---------------------------------------------------------
// STREAK_START_NEXT
// Was going to put it in a separate function but feels too contextual.
= AND( NOT(_Y(-1)), XY(1,-1) )

= LAMBDA(up, rightUp,
      AND( NOT(up), rightUp )
    )(
      _Y(-1),
      XY(1,-1)
    )

// ---------------------------------------------------------
// PREV_DAY
// Returns the value of the day to the left, jumping left once more if the left col is a week col. Assumes you're below a day checkbox.
=LAMBDA(left, 
  IF(left = "day",
    _X(-1),
    IF( left = "week",
      _X(-2),
      0
    ))
)(GET_COL_LABEL(COL()-1))

// ---------------------------------------------------------
// IS_DAY(col_index)
// Is the given col_index a 'day'?
=GET_COL_LABEL(col_index) = "day"

// ---------------------------------------------------------
// GET_COL_LABEL(col_index)
// Get the col label of the given col index.
// col_index = Column index. >1
=XY_ABS(col_index, colLabelRow)

// ---------------------------------------------------------
// SET_COL_LABEL(cell_ref)
// Set the col label (day/week) of the given cell reference. Empty if none of the above.
=IF( Y_ABS_REF(cell_ref, weekNumRow) <> "",
  "week",
  IF( Y_ABS_REF(cell_ref, weekdayRow) <> "",
    "day",
  ))
=IF( EQ(LEFT(Y_ABS_REF(cell_ref, weekNumRow)), "w"),
  "week",
  IF( CONTAINS(Y_ABS_REF(cell_ref, weekdayRow), weekdays),
    "day",
  )
)
// ---------------------------------------------------------
// X_ABS_REF(cell_ref, x)
// Return the value of the cell on the same row to the given cell reference but x cols right from the left edge of the page. y can only be positive. Variant of X_ABS to use with MAP.
// x = How many cols to move right from the left edge of the page, at the row of the given cell.
=INDIRECT(
  "R"& ROW(cell_ref) &""&
  "C"& x &"", 
  FALSE)
// ---------------------------------------------------------
// Y_ABS_REF(cell_ref, y)
// Return the value of the cell on the same column to the given cell reference but y rows down from the top edge of the page. y can only be positive. Variant of Y_ABS to use with MAP.
// y = How many rows to move down from the top edge of the page, at the column of the given cell.
=INDIRECT(
  "R"& y &""&
  "C"& COL(cell_ref) &"", 
  FALSE)

// ---------------------------------------------------------
// Y_ABS(y)
// Return the value of the cell on the same column that current cell but x rows down from the top edge of the page. y can only be positive. Variant of XY_ABS.
// y = How many rows to move down from the top edge of the page, at the column of the current cell.
=INDIRECT(
  "R"& y &""&
  "C"& COL() &"", 
  FALSE)

// ---------------------------------------------------------
// X_ABS(x)
// Return the value of the cell on the same row that current cell but x columns right from the left edge of the page. x can only be positive. Variant of XY_ABS.
// x = How many columns to move right from the left edge of the page, at the row of the current cell.
=INDIRECT(
  "R"& ROW() &""&
  "C"& x &"", 
  FALSE)

// ---------------------------------------------------------
Tabs:
* streaks
* historic
* lib
* formulas